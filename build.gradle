buildscript {
    repositories {
        mavenLocal()
    }

    ext {
        kutnaCommonGroupId = 'com.chadmarchand.kutna'
        kutnaCommonArtifactId = 'kutna-common'
        kutnaCommonVersion = '1.0.19'
        kutnaCommonVcsRepoUrl = 'https://github.com/chadmarchand/kutna-common.git'
    }

    dependencies {
        classpath 'com.chadmarchand.kutna:kutna-gradle-plugin:1.0.+'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
}

apply plugin: com.chadmarchand.kutna.gradle.KutnaGradlePlugin

repositories {
    mavenCentral()
    jcenter()
}

group "$kutnaCommonGroupId"
version "$kutnaCommonVersion"

dependencies {
    implementation 'org.springframework:spring-context:5.2.3.RELEASE'
}

configurations {
    testArtifacts.extendsFrom testCompile
}

task incrementPatchVersion << {
    String patchVersion = version.substring(version.lastIndexOf('.') + 1)
    String majorAndMinorVersion = version.substring(0, version.lastIndexOf('.') + 1)
    int newPatchVersion = patchVersion.toInteger() + 1

    String updatedBuildFileText = buildFile
            .getText()
            .replaceFirst(
                    "kutnaCommonVersion = '$kutnaCommonVersion'",
                    "kutnaCommonVersion = '"+majorAndMinorVersion+newPatchVersion+"'"
            )
    buildFile.setText(updatedBuildFileText)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar, dependsOn: classes) {
    from sourceSets.test.output
    classifier "test"
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    testArtifacts testJar
}

def pomConfig = {
    licenses {
        license([:]) {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license"
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'chadmarchand'
            name 'Chad Marchand'
        }
    }

    scm {
        url "$kutnaCommonVcsRepoUrl"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact testJar {
                classifier "test"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId "$kutnaCommonGroupId"
            artifactId "$kutnaCommonArtifactId"
            version "$kutnaCommonVersion"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Common types and utils used with the Kutna framework for Kotlin')
                root.appendNode('name', "$kutnaCommonArtifactId")
                root.appendNode('url', "$kutnaCommonVcsRepoUrl")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    publish = true

    pkg {
        repo = "$kutnaCommonArtifactId"
        name = "$kutnaCommonArtifactId"
        licenses = ['MIT']
        vcsUrl = "$kutnaCommonVcsRepoUrl"
        version {
            name = "$kutnaCommonVersion"
            desc = "$kutnaCommonVersion"
            released  = new Date()
        }
    }
}
