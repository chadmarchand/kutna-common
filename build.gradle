buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    ext {
        kutnaCommonGroupId = 'com.chadmarchand'
        kutnaCommonArtifactId = 'kutna-common'
        kutnaCommonVersion = '1.0.14'
        kutnaCommonVcsRepoUrl = 'https://github.com/chadmarchand/kutna-common.git'

        jvmVersion = '1.8'
        koinVersion = '2.1.6'
        junitVersion = '5.4.2'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

group "$kutnaCommonGroupId"
version "$kutnaCommonVersion"

sourceCompatibility = "$jvmVersion"

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    implementation "org.koin:koin-core:$koinVersion"
    implementation "org.koin:koin-core-ext:$koinVersion"
    testImplementation "org.koin:koin-test:$koinVersion"
    implementation "org.koin:koin-java:2.1.0-alpha-2"

    implementation 'org.springframework:spring-context:5.2.3.RELEASE'
}

compileKotlin {
    kotlinOptions.jvmTarget = "$jvmVersion"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "$jvmVersion"
}

configurations {
    testArtifacts.extendsFrom testCompile
}

task incrementPatchVersion << {
    String patchVersion = version.substring(version.lastIndexOf('.') + 1)
    String majorAndMinorVersion = version.substring(0, version.lastIndexOf('.') + 1)
    int newPatchVersion = patchVersion.toInteger() + 1

    String updatedBuildFileText = buildFile
            .getText()
            .replaceFirst(
                    "kutnaCommonVersion = '$kutnaCommonVersion'",
                    "kutnaCommonVersion = '"+majorAndMinorVersion+newPatchVersion+"'"
            )
    buildFile.setText(updatedBuildFileText)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar, dependsOn: classes) {
    from sourceSets.test.output
    classifier "test"
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    testArtifacts testJar
}

def pomConfig = {
    licenses {
        license([:]) {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license"
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'chadmarchand'
            name 'Chad Marchand'
        }
    }

    scm {
        url "$kutnaCommonVcsRepoUrl"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact testJar {
                classifier "test"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId "$kutnaCommonGroupId"
            artifactId "$kutnaCommonArtifactId"
            version "$kutnaCommonVersion"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Common types and utils used with the Kutna framework for Kotlin')
                root.appendNode('name', "$kutnaCommonArtifactId")
                root.appendNode('url', "$kutnaCommonVcsRepoUrl")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    publish = true

    pkg {
        repo = "$kutnaCommonArtifactId"
        name = "$kutnaCommonArtifactId"
        licenses = ['MIT']
        vcsUrl = "$kutnaCommonVcsRepoUrl"
        version {
            name = "$kutnaCommonVersion"
            desc = "$kutnaCommonVersion"
            released  = new Date()
        }
    }
}
