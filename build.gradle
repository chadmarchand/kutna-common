buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    ext {
        kutnaCommonGroupId = 'com.chadmarchand'
        kutnaCommonArtifactId = 'kutna-common'
        kutnaCommonVersion = '1.0.4'

        vcsRepoUrl = 'https://github.com/chadmarchand/kutna-common.git'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

group "$kutnaCommonGroupId"
version "$kutnaCommonVersion"

sourceCompatibility = 1.8

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license([:]) {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license"
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'chadmarchand'
            name 'Chad Marchand'
        }
    }

    scm {
        url "$vcsRepoUrl"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId "$kutnaCommonGroupId"
            artifactId "$kutnaCommonArtifactId"
            version "$kutnaCommonVersion"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Common types and utils used with the Kutna framework for Kotlin')
                root.appendNode('name', "$kutnaCommonArtifactId")
                root.appendNode('url', "$vcsRepoUrl")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = "$kutnaCommonArtifactId"
        name = "$kutnaCommonArtifactId"
        licenses = ['MIT']
        vcsUrl = "$vcsRepoUrl"
        version {
            name = "$kutnaCommonVersion"
            desc = "$kutnaCommonVersion"
            released  = new Date()
        }
    }
}
